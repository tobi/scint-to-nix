# onix examples — Justfile
# Run from examples/ directory

set shell := ["bash", "-euo", "pipefail", "-c"]

ruby := "ruby_3_4"
gem := "ruby -I../lib ../exe/onix"

# ── Build ──────────────────────────────────────────────────────────

# Build gems: all, by app, or a single gem
[group('build')]
build *args:
    RUBY={{ruby}} {{gem}} build {{args}}

# ── Generate ───────────────────────────────────────────────────────

# Fetch all gem sources into cache/
[group('generate')]
fetch *args:
    {{gem}} fetch {{args}}

# Generate all Nix derivations + run checks
[group('generate')]
generate:
    {{gem}} generate

# Import a project (name or path to Gemfile.lock)
[group('generate')]
import *args:
    {{gem}} import {{args}}

# Recreate source symlinks (after fresh clone)
[group('generate')]
link:
    #!/usr/bin/env bash
    n=0
    for d in nix/gem/*/*/; do
        [[ -d "$d" ]] || continue
        [[ "$(basename "$d")" == git-* ]] && continue
        name=$(basename "$(dirname "$d")")
        version=$(basename "$d")
        target="cache/sources/${name}-${version}"
        link="${d}source"
        if [[ -d "$target" ]]; then
            rm -f "$link"
            ln -s "$(cd "$target" && pwd)" "$link"
            n=$((n + 1))
        fi
    done
    echo "Linked $n source directories."

# ── Test & Lint ────────────────────────────────────────────────────

# Run lint
[group('test')]
lint *args:
    {{gem}} lint {{args}}

# Run an app's test suite
[group('test')]
test app:
    tests/{{app}}/run-tests

# Run all app tests
[group('test')]
test-all:
    tests/run-all
